新的超短线突破战法：
选股条件1，来自通达信的公式：{ZXB1+砖形图}
VAR1A:=(HHV(HIGH,4)-CLOSE)/(HHV(HIGH,4)-LLV(LOW,4))*100-90;
VAR2A:=SMA(VAR1A,4,1)+100;
VAR3A:=(CLOSE-LLV(LOW,4))/(HHV(HIGH,4)-LLV(LOW,4))*100;
VAR4A:=SMA(VAR3A,6,1);
VAR5A:=SMA(VAR4A,6,1)+100;
VAR6A:=VAR5A-VAR2A;
砖型图:=IF(VAR6A>4,VAR6A-4,0),COLORRED;

N:=20;
M:=50;

趋势白线:=EMA(EMA(C,10),10);
大哥黄线:=(MA(CLOSE,M1)+MA(CLOSE,M2)+MA(CLOSE,M3)+MA(CLOSE,M4))/4;
SHORT :=100*(C-LLV(L,N1))/(HHV(C,N1)-LLV(L,N1));
LONG :=100*(C-LLV(L,N2))/(HHV(C,N2)-LLV(L,N2));
{===以上如需Z哥公式请自行填入===}
BBI:=(MA(CLOSE,3)+MA(CLOSE,6)+MA(CLOSE,12)+MA(CLOSE,24))/4;


{定义基础判定信息}
振幅区间 := IF(CODELIKE('68') OR CODELIKE('30') OR CODELIKE('4') OR CODELIKE('8') OR CODELIKE('9') OR EXIST(C/REF(C,1)>1.15,200), 8, 5);
放宽系数 :=IF(CODELIKE('68') OR CODELIKE('30') OR CODELIKE('4') OR CODELIKE('8') OR CODELIKE('9') OR EXIST(C/REF(C,1)>1.15,200),0.9,1);
当日振幅 := (HIGH - LOW) / LOW * 100;
当日涨跌幅 := ABS(CLOSE - REF(CLOSE, 1)) / REF(CLOSE, 1) * 100 * 放宽系数;
上涨十字星 := C>REF(C,1) AND (ABS(C-O)/O*100 * 放宽系数)<1.8;
单针下20 := (SHORT<=20 AND LONG>=75) OR ((LONG-SHORT)>=70);
聚宝盆 := COUNT(LONG>=75,8)>=6 AND COUNT(SHORT<=70,7)>=4 AND COUNT(SHORT<=50,8)>=1;
双叉戟 := EVERY(LONG>=75,8) AND COUNT(SHORT<=50,6)>=2 AND COUNT(SHORT<=20,7)>=1;
红肥绿瘦 := COUNT(C>=O,15)>7 OR COUNT(C>REF(C,1),11)>5;
{定义大绿棒和缩量条件}
VDAY := HHVBARS(VOL, 40); {40天最大成交量日}
不是大绿棒 := REF(C,VDAY)>=REF(C,VDAY+1) OR REF(C,VDAY)>=REF(O,VDAY);
大绿棒 :=NOT(不是大绿棒);
大绿棒离得远:= VDAY>=15 AND 大绿棒;
缩量:=(VOL < HHV(VOL, 20) *0.416) OR (VOL < HHV(VOL, 50) / 3);{今日缩量}
回踩缩量:=(VOL < HHV(VOL, 20) *0.45) OR (VOL < HHV(VOL, 50) / 3);{回踩白线缩量适度放宽}
适当缩量:=(VOL < HHV(VOL, 20) *0.618) OR (VOL < HHV(VOL, 50) / 3);{用于超牛股回踩，更放宽}
超缩量:=(VOL < HHV(VOL, 30)/4) OR (VOL < HHV(VOL, 50) / 6);{今日超级缩量}

{KDJ计算}
J:=KDJ.J,COLORAE4FD9;
K:=KDJ.K;
{3日RSI计算}
LC:=REF(CLOSE,1);
TEMP1:=MAX(CLOSE-LC,0);
TEMP2:=ABS(CLOSE-LC);
RSI:=SMA(TEMP1,3,1)/SMA(TEMP2,3,1)*100,COLORFFA019;
{ 定义振幅 }
LOWN := LLV(LOW, N);
HIGHN := HHV(HIGH, N);
近期振幅 := (HIGHN - LOWN) / LOWN * 100;
近期异动 := 近期振幅 >= 15 OR (HHV(H,12)-LLV(L,14))/LLV(L,14) * 100>=11;
LOWM := LLV(LOW, M);
HIGHM := HHV(HIGH, M);
远期振幅 := (HIGHM - LOWM) / LOWM * 100;
远期异动 := 远期振幅 >=30;
超级异动 := 近期振幅 >=60;
洗盘异动 := (COUNT(单针下20,10)>=2) OR (聚宝盆) OR 双叉戟;

{定义趋势股}
做上涨趋势 := 趋势白线>=大哥黄线*0.999 AND (C>=大哥黄线 OR (C>大哥黄线*0.975 AND C>O)) ;
强趋势股 := EVERY(大哥黄线>=REF(大哥黄线,1)*0.999,13) AND 趋势白线>=REF(趋势白线,1) AND EVERY(趋势白线>大哥黄线,20) AND EVERY(趋势白线>=REF(趋势白线,1),11) AND 红肥绿瘦;
超牛股 := (EVERY(BBI>=REF(BBI,1)*0.999,20) OR COUNT(BBI>=REF(BBI,1),25)>=23) AND (近期振幅 >=30 OR 远期振幅>80) AND BARSLAST(CROSS(C,大哥黄线))>12;

{定义回踩白线}
距离白线:=ABS(C-趋势白线)/C*100;
L距离白线:=(ABS(L-趋势白线)/趋势白线)*100;
距离BBI:=ABS(C-BBI)/C*100;
L距离BBI:=(ABS(L-BBI)/BBI)*100;
回踩白线:=(C>=趋势白线 AND 距离白线<=2) OR (C<趋势白线 AND 距离白线<0.8) OR (C>=BBI AND 距离BBI<2.5 AND L距离BBI<1 AND 距离白线<=3 AND 当日涨跌幅<1 AND C>REF(C,1));
白线支撑:=C>=趋势白线 AND 距离白线<1.5;
强势回踩不破:=(L距离白线<1 OR L距离BBI<0.5) AND (C>趋势白线) AND (距离白线<=3.5);

{定义回踩黄线}
距离黄线:=(ABS(C-大哥黄线)/大哥黄线)*100;
回踩黄线:=(C>=大哥黄线 AND (距离黄线<=1.5 OR (距离黄线<=2 AND 当日涨跌幅<1))) OR (C<大哥黄线 AND 距离黄线<=0.8);

{判定买入提示，基本原理：少妇缩量B1十字星，小振幅}
{黄色RSI拐头，红色缩量B1，青色超级缩量B1}
超卖缩量拐头B:=做上涨趋势 AND (RSI-15)>=REF(RSI,1) AND (REF(RSI,1)<20 OR REF(J,1)<14) AND 当日振幅<(振幅区间+0.5) AND (当日涨跌幅<2.3 OR (上涨十字星 AND 当日涨跌幅<4)) AND (不是大绿棒 OR 大绿棒离得远) AND (近期异动 OR 远期异动 OR 洗盘异动) AND C>=大哥黄线;
超卖缩量B:=做上涨趋势 AND (J<14 OR RSI<23) AND (RSI+J<55 OR J=LLV(J,20)) AND 当日振幅<振幅区间 AND (当日涨跌幅<2.5 OR 上涨十字星) AND (不是大绿棒 OR 大绿棒离得远) AND (缩量 OR (适当缩量 AND 当日涨跌幅<1))  AND (近期异动 OR 远期异动 OR 洗盘异动);
原始B1:=趋势白线>大哥黄线 AND C>=大哥黄线*0.99 AND 大哥黄线>=REF(大哥黄线,1) AND (J<13 OR RSI<21) AND (RSI+J)<LLV(RSI+J,15)*1.5 AND 适当缩量 AND (不是大绿棒 OR 大绿棒离得远) AND (ABS(C-O)*100/O<1.5 OR (超缩量 OR (适当缩量 AND V<LLV(V,20)*1.1 AND J=LLV(J,20))) OR(适当缩量 AND (距离白线<1.8  OR 距离BBI<1.5 OR 距离黄线<2.8))) AND (近期异动 OR 远期异动 OR 洗盘异动);
超卖超缩量B:=做上涨趋势 AND (J<14 OR RSI<23) AND RSI+J<60 AND 远期振幅 >=45 AND (当日振幅<振幅区间 OR (超级异动 AND 当日振幅<振幅区间+3.2 AND C>O AND C>趋势白线)) AND ((C<O AND V< REF(V,1) AND C>=大哥黄线) OR (C>=O)) AND (当日涨跌幅<2 OR 上涨十字星) AND (不是大绿棒 OR 大绿棒离得远) AND 超缩量 AND (近期异动 OR 远期异动 OR 洗盘异动);
回踩白线B:=强趋势股 AND (J<30 OR RSI<40 OR 洗盘异动) AND RSI+J<70 AND (当日振幅<振幅区间+0.5 OR 距离白线<1 OR 距离BBI<1) AND 回踩白线 AND (当日涨跌幅<2 OR (当日涨跌幅<5 AND 白线支撑)) AND (不是大绿棒 OR 大绿棒离得远) AND 回踩缩量 AND (近期异动 OR 远期异动 OR 洗盘异动) AND L<=REF(C,1);
回踩超级B := 超牛股 AND (J<35 OR RSI<45 OR 洗盘异动) AND RSI+J<80 AND (RSI+J)=LLV(RSI+J,25) AND 当日振幅<振幅区间+1 AND (当日涨跌幅<2.5 OR 距离白线<2) AND 强势回踩不破 AND (不是大绿棒 OR 大绿棒离得远) AND (近期异动 OR 远期异动 OR 洗盘异动) AND 适当缩量;
回踩黄线B := 趋势白线>=大哥黄线 AND C>=大哥黄线*0.975 AND (J<13 OR RSI<18) AND 回踩黄线 AND (不是大绿棒 OR 大绿棒离得远) AND (缩量 OR (适当缩量 AND (J=LLV(J,20) OR RSI=LLV(RSI,14)))) AND 大哥黄线>=REF(大哥黄线,1)*0.997 AND MA(C,60)>=REF(MA(C,60),1) AND 近期振幅>=11.9 AND 远期振幅>=19.5;
存在B:=超卖缩量拐头B OR 超卖缩量B OR 原始B1 OR 超卖超缩量B OR 回踩白线B OR 回踩超级B OR 回踩黄线B;
{===}
RSI3日:=SMA(TEMP1,3,1)/SMA(TEMP2,3,1)*100,COLORFFA019; {计算3日版本RSI}
N1:=KDJ.J-REF(KDJ.J,1);
N2:=RSI3日-REF(RSI3日,1);
成交量系数:=IF(V<REF(V,1)*0.99,(1-5*(REF(V,1)-V)/REF(V,1))*0.8,1);{与前日阴线对比，今日阳线缩量，则扣分}
倍量系数:=IF(V/REF(V,1)>=4,1.4,(0.1*V/REF(V,1))+1);
倍量系数加成:=IF(C>O AND C>REF(C,1) AND V>REF(V,1)*1.8,倍量系数,1);
影线系数:=IF(C>REF(C,1) AND C>O,(0.75-(H-C)/(H-MIN(O,REF(C,1))))*1.3,1);{上影线长则扣分，下跌不计算影线}

J动能:=N1,COLORAE4FD9;{今日J与前日差值，越大越好}
R动能:=N2,COLORFFA019;{今日RSI与前日差值，越大越好}
黄柱:=(N1+N2)/2*影线系数*倍量系数加成;
X动能:=IF((C>O AND C>REF(C,1) AND (N1+N2)>(REF(N1,1)+REF(N2,1))),((N1+N2)-(REF(N1,1)+REF(N2,1)))/2*影线系数*成交量系数*倍量系数加成,0);

{===}
{红绿判定}
今红:=砖型图>REF(砖型图,1);
今绿:=砖型图<=REF(砖型图,1);
昨绿:=REF(今绿,1)=1;
{红绿长度计算}
红柱长度:=IF(今红,砖型图-REF(砖型图,1),0);
长度:=砖型图-REF(砖型图,1);
昨绿长度:=IF(昨绿,REF(砖型图,2)-REF(砖型图,1),0);
{红绿比值计算}
比值:=IF(昨绿长度>0,红柱长度/昨绿长度,0);
{强红判定}

强红:=今红 AND 昨绿 AND 比值>0.666;

趋势条件:=趋势白线>=大哥黄线*0.995 AND 大哥黄线>=REF(大哥黄线,1)*0.997 AND C>=大哥黄线*0.997;
上影线条件:=(C>=O OR C>REF(C,1)) AND (1-(H-C)/(H-MIN(L,REF(C,1))))>0.618;
换手条件:=DYNAINFO(37)>=0.0099;
共振条件1:= 强红 AND (黄柱>=7.5 OR X动能>=7.5) AND (EXIST(存在B=1,2) OR (REF(LONG,1)>85 AND REF(SHORT,1)<30));
共振条件2:= 强红 AND (黄柱>=10 OR X动能>=10) AND ((EXIST(LONG-SHORT>60,4) AND LONG>98 AND SHORT>98) OR (黄柱>20 AND C>趋势白线) OR 黄柱>30 OR (黄柱+长度)>50 OR X动能>40);
共振条件:=共振条件1 OR 共振条件2;
{买入条件1:=共振条件1 AND 上影线条件 AND 趋势条件 AND 换手条件;
买入条件2:=共振条件2 AND 上影线条件 AND 趋势条件 AND 换手条件;}
买入条件:=共振条件 AND 上影线条件 AND 趋势条件 AND 换手条件;

XG:买入条件;
其中参数为M1=14 M2=28 M3=57 M4=114 N1=3 N2=21

选股条件2，也来自通达信公式：
{==== 参数区（可改） ====}

UPPCT:=4;          {长阳涨幅阈值，单位：% ；例如 4 表示 >4%}

WICKMAX:=0.03;      {上影线比例上限，例如 0.5 表示 <0.5}

N:=20;             {均量回看天数}

VOLMULT:=1.5;      {放量倍数阈值}

ZXMULT:=1.15;       {收盘 < 短期成本线 * 倍数，例如 1.15 表示允许略高于ZXDQ}{==== 1) 禁止假阴真阳 ====}

BULL:=C>=O;{==== 2) 长阳：涨幅 > 阈值 ====}

BIGUP:=(C/REF(C,1)-1)*100 > UPPCT;{==== 3) 上影线比例 < 阈值 ====}

WICK:=(H-MAX(O,C))/MAX(O,C);

WICKOK:=WICK < WICKMAX;{==== 4) 放量：V > 前N日均量 * 倍数 ====}

AVGVOL:=MA(V,N);

VOLOK:=V > VOLMULT*AVGVOL;{==== 5) ZXDQ 约束：C < ZXDQ * 倍数 ====}

ZXDQ:=EMA(EMA(C,10),10);

ZXOK:=C < ZXDQ*ZXMULT;{==== 最终条件 ====}

XG: BULL AND BIGUP AND WICKOK AND VOLOK AND ZXOK;

必须满足同时满足两个条件才可以。
第T个交易日收盘后运行该选股条件选股，第T+1交易日开盘买入。止盈条件：收益大于10%或持有满3个交易日就卖出；止损条件：当天收盘跌破买入价就按当天收盘价卖出。